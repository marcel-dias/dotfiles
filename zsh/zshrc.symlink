#!/bin/zsh
# shortcut to this dotfiles path is $ZSH
export ZSH="$HOME/.dotfiles"

# your project folder that we can `c [tab]` to
export PROJECTS="$HOME/Code"

# your default editor
export EDITOR='vim'

# all of our zsh files
typeset -U config_files
config_files=($ZSH/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load starship
if test "$(which startship)"; then
  source <(starship init zsh)
fi

# load everything less the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

if type brew &>/dev/null
then
  FPATH="$(brew --prefix)/share/zsh/site-functions:${FPATH}"
fi

## load zsh plugins installed with homebrew
zsh_plugins=($(brew --prefix)/share/*/*.zsh)
for file in ${zsh_plugins}; do
  source "$file"
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
if [ ! -f ~/.zcompdump ] || [ $(date +'%j') != $(stat -f '%Sm' -t '%j' ~/.zcompdump) ]; then
  compinit -i
else
  compinit -C -i
fi

autoload -U colors; colors

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [ ! -f ~/.localrc ] || [ $(date +'%j') != $(stat -f '%Sm' -t '%j' ~/.localrc) ]; then
  if test "$(which op)"; then
    op read -o ~/localrc.env op://Personal/CLI_VARs/notes -f 1> /dev/null
    op inject -f -i ~/localrc.env -o ~/.localrc 1> /dev/null
  fi
fi

[[ -a ~/.localrc ]] && source ~/.localrc

# iterm shell integration
test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh" || true
